@	Self Service Boons
@	Coded by: Gizmo AKA Ourea, Alzie, Oukranos
@	Email: ccubed.techno@gmail.com
@	Date Last Touched: 1/28/15
@----------------------------------------------------------------------------
@	Purpose: Self Service Boons Commands
@----------------------------------------------------------------------------
@	Commands:
@	+boons - Display your outstanding boon totals
@	+boons/report - Display an indepth list of the people you owe boons to or that owe you boons along with the last reason a boon was added and amounts
@	+boons/credit # - If you have more than 10 entries, they're paginated. This allows you to view a specific page of entries for Boons owed to you.
@	+boons/debt # - If you have more than 10 entries, they're paginated. This allows you to view a specific page of entries for Boons you owe.
@	+boons/detail # - Show details on a boon group. Shows each boon that adds to the total and what it was given for.
#	+boons/add Name/Amt and Type=Reason - This says that you owe Name Amt And Type of boons for reason.
@	+boons/claim Name/Amt and Type=Reason - Claim that Name owes you Amt and Type of boons for reason. Amt and Type in following format: #M#m#t. In any order. Major, minor, trivial
@	+boons/callin ID/Amt and Type=Reason - Call in Amt and Type of Boon from ID for Reason. This opens a job between you, the other player and the harpy to discuss and then move forward.
@	+boons/trade Name/ID=Amt and Type/Reason - Trade Amt and Type of boons from ID to Name for Reason
@	+boons/praxis - The praxis overview of boons. Shows a sum of all major, minor and trivial boons in the praxis.
@	+boons/search Type=Name - Search boons for Type by Name. Type - Debt, Credit. Name - Name of person to search by
@	+boons/search Field=Term/Num -  Search boons for Type by Name. Type - Debt, Credit. Name - Name of person to search by. Start on Page Num
@	+boons/unapproved - Show unapproved boons
@	+boons/approve ID - Approve an unapproved boon and slap it in that DB
@	+boons/deny ID=Reason - Deny an unapproved boon for reason. Sends mail to both people.
@	+boons/closejob Job Number=<0/1>/<reasaon> - Close a job in the boon bucket for harpies. 0 will deny the called in boon but requires a reason. 1 will approve it and requires no reason.
@	This will only work for the boons bucket.
@
@	Notes:
@	approved = 0 (unapproved), 1 (approved), 2 (denied)
@
@	@sql CREATE TABLE boons (id INT(11) NOT NULL PRIMARY KEY AUTO_INCREMENT, name VARCHAR(255) NOT NULL, reason TEXT NOT NULL, to_name VARCHAR(255) NOT NULL, approved INT(11) NOT NULL
@	DEFAULT 0, trivial INT(11) NOT NULL DEFAULT 0, minor INT(11) NOT NULL DEFAULT 0, major INT(11) NOT NULL DEFAULT 0, transactions TEXT)
@

&SQL`INSERT_BOON #2885=INSERT INTO boons (name,reason,to_name,approved,trivial,minor,major) VALUES ('[sqlescape(%0)]','[sqlescape(%1)]','[sqlescape(%2)]',[sqlescape(%3)],[sqlescape(%4)],[sqlescape(%5)],[sqlescape(%6)])

&SQL`GET_HIGHEST_BOON #2885=SELECT COUNT(*) FROM boons

&SQL`GET_SOME_ROWS #2885=SELECT * FROM boons WHERE id > [sub([sqlescape(%0)],1)] AND id < [add([sqlescape(%1)],1)]

&SQL`GET_UNAPPROVED #2885=SELECT * FROM boons WHERE approved = 0

&SQL`GET_MY_BOONS_DEBT #2885=SELECT * FROM boons WHERE name = '[sqlescape(%0)]'

&SQL`GET_MY_BOONS_CREDIT #2885=SELECT * FROM boons WHERE to_name = '[sqlescape(%0)]'

&SQL`GET_ALL_MY_BOONS #2885=SELECT * FROM boons WHERE name = '[sqlescape(%0)]' OR to_name = '[sqlescape(%0)]'

&SQL`DO_THEY_HAVE_BOONS #2885=SELECT COUNT(*) FROM boons WHERE name = '[sqlescape(%0)]' OR to_name = '[sqlescape(%0)]'

&SQL`CHECK_BOON_BALANCE_EXISTS #2885=SELECT * FROM boons WHERE name = '[sqlescape(%0)]' AND to_name = '[sqlescape(%1)]'

&SQL`ARCHIVE_BOON_BANK #2885=INSERT INTO boons_archive SELECT * FROM boons WHERE id = [sqlescape(%0)]

&SQL`GET_SPECIFIC_ID #2885=SELECT * FROM boons WHERE id = [sqlescape(%0)]

&SQL`GET_ALL_BOONS #2885=SELECT * FROM boons

&SQL`UPDATE_EXISTING #2885=UPDATE boons SET reason='[sqlescape(%0)]', trivial=[sqlescape(%1)], minor=[sqlescape(%2)], major=[sqlescape(%3)], transactions='[sqlescape(%4)]' WHERE id=[sqlescape(%5)]

&DATA`HARPIES #2885=#1798 #2668 #2121

&CMD`BOONS #2885=$+boons:@pemit %#=[if([sql(u(SQL`DO_THEY_HAVE_BOONS,[name(%#)]))],[u(DISP`BOON.OVERVIEW,%#)],[header(Boons)]%R%R[center([ansi(hw,You have no boons.)],78)]%R)]%R[header(Type +boons/report for Details)]

&DISP`BOON.OVERVIEW #2885=[header(Outstanding Boons Owed to You)]%R[center([ansi(hw,Trivial)],26)][center([ansi(hw,Minor)],26)][center([ansi(hw,Major)],26)]%R%b[repeat(-,76)]%R[center([if(setr(a,[sql(SELECT SUM(trivial) FROM boons WHERE to_name = '[sqlescape([name(%0)])]')]),%qA,0)],26)][center([if(setr(a,[sql(SELECT SUM(minor) FROM boons WHERE to_name = '[sqlescape([name(%0)])]')]),%qA,0)],26)][center([if(setr(a,[sql(SELECT SUM(major) FROM boons WHERE to_name = '[sqlescape([name(%0)])]')]),%qA,0)],26)]%R%R[header(Outstanding Boons You Owe)]%R[center([ansi(hw,Trivial)],26)][center([ansi(hw,Minor)],26)][center([ansi(hw,Major)],26)]%R%b[repeat(-,76)]%R[center([if(setr(a,[sql(SELECT SUM(trivial) FROM boons WHERE name = '[sqlescape([name(%0)])]')]),%qA,0)],26)][center([if(setr(a,[sql(SELECT SUM(minor) FROM boons WHERE name = '[sqlescape([name(%0)])]')]),%qA,0)],26)][center([if(setr(a,[sql(SELECT SUM(major) FROM boons WHERE name = '[sqlescape([name(%0)])]')]),%qA,0)],26)]%R

&CMD`BOONS.REPORT #2885=$+boons/report:@pemit %#=[if([sql(u(SQL`DO_THEY_HAVE_BOONS,[name(%#)]))],[u(DISP`BOON.DETAILS,%#)],[header(Boons)]%R%R[center([ansi(hw,You have no boons.)],78)]%R)]

&DISP`BOON.DETAILS #2885=[setq(A,[sql(u(SQL`GET_MY_BOONS_DEBT,[name(%0)]),:,~)])][setq(B,[sql(u(SQL`GET_MY_BOONS_CREDIT,[name(%0)]),:,~)])][header(Outstanding Boons Owed to You)]%R[u(DISP`DETAILS.HEADER)]%R[switch([gt(words(%qB,:),10)],1,[iter(%qB,[if(lte(inum(0),10),[u(DISP`BOON.ROW.CREDIT,%i0)])],:,%R)][footer(Pg. 1/[u(F`CALC.PAGES,[words(%qB,:)])]. Use +boons/credit # for more)],0,[iter(%qB,[u(DISP`BOON.ROW.CREDIT,%i0)],:,%R)])]%R%R[header(Outstanding Boons You Owe)]%R[u(DISP`DETAILS.HEADER)]%R[switch([gt(words(%qA,:),10)],1,[iter(%qA,[if(lte(inum(0),10),[u(DISP`BOON.ROW.DEBT,%i0)])],:,%R)]%R[footer(Pg. 1/[u(F`CALC.PAGES,[words(%qA,:)])]. Use +boons/debt # for more)],0,[iter(%qA,[u(DISP`BOON.ROW.DEBT,%i0)],:,%R)]%R%R[footer()])]

&DISP`DETAILS.HEADER #2885=[ljust(ID,5)][ljust(Name,10)][ljust(Reason,33)][ljust(Trivial,10)][ljust(Minor,10)][ljust(Major,10)]%R%b[repeat(-,76)]

&F`CALC.PAGES #2885=[setq(0,[div(%0,10)])][setq(1,[mod(%0,10)])][if(%q1,[setq(2,[add(%q0,%q1)])]%q2,%q0)]

&DISP`BOON.ROW.DEBT #2885=[ljust([extract(%0,1,1,~)],5)][ljust([extract(%0,4,1,~)],10)][ljust([before([last([extract(%0,3,1,~)],|)],/)],33)][ljust([extract(%0,6,1,~)],10)][ljust([extract(%0,7,1,~)],10)][ljust([extract(%0,8,1,~)],10)]

&DISP`BOON.ROW.CREDIT #2885=[ljust([extract(%0,1,1,~)],5)][ljust([extract(%0,2,1,~)],10)][ljust([before([last([extract(%0,3,1,~)],|)],/)],33)][ljust([extract(%0,6,1,~)],10)][ljust([extract(%0,7,1,~)],10)][ljust([extract(%0,8,1,~)],10)]

&CMD`BOONS.CREDIT.PAGE #2885=$+boons/credit *:@assert [cand(isnum(%0),lte(%0,[u(F`CALC.PAGES,[words([setr(A,sql(u(SQL`GET_MY_BOONS_CREDIT,[name(%#)]),:,~))],:)])]))]={@pemit %#=[ansi(hr,ERR:)]%b%0 is either not a number or more than the pages available. There are [u(F`CALC.PAGES,[words(%qA,:)])] pages available.};@pemit %#=[u(DISP`BOONS.PAGES,%qA,%#,[mul(10,%0)],0,%0)]

&CMD`BOONS.DEBT.PAGE #2885=$+boons/debt *:@assert [cand(isnum(%0),lte(%0,[u(F`CALC.PAGES,[words([setr(A,sql(u(SQL`GET_MY_BOONS_DEBT,[name(%#)]),:,~))],:)])]))]={@pemit %#=[ansi(hr,ERR:)]%b%0 is either not a number or more than the pages available. There are [u(F`CALC.PAGES,[words(%qA,:)])] pages available.};@pemit %#=[u(DISP`BOONS.PAGES,%qA,%#,[mul(10,%0)],1,%0)]

&DISP`BOONS.PAGES #2885=[switch(%3,0,[header(Outstanding Boons Owed to [if(%5,%5,You)])]%R[u(DISP`DETAILS.HEADER)]%R[iter(lnum(1,10),[u(DISP`BOON.ROW.CREDIT,[extract(%0,[add(%2,inum(0))],1,:)])], ,%R)]%R[if(lt(%4,u(F`CALC.PAGES,words(%0,:))),[footer(P.%0/[u(F`CALC.PAGES,words(%0,:))])],[footer()])],1,[header(Outstanding Boons [if(%5,%5 Owes,You Owe)])]%R[u(DISP`DETAILS.HEADER)]%R[iter(lnum(1,10),[u(DISP`BOON.ROW.DEBT,[extract(%0,[add(%2,inum(0))],1,:)])], ,%R)]%R[if(lt(%4,u(F`CALC.PAGES,words(%0,:))),[footer(P.%0/[u(F`CALC.PAGES,words(%0,:))])],[footer()])])]

&CMD`BOON.BANK.DETAILS #2885=$+boons/detail *:@assert [t([setr(A,[sql(u(SQL`GET_SPECIFIC_ID,%0),:,~)])])]={@pemit %#=[ansi(hr,ERR:)]%bCan't find ID %0.};@assert [cor(strmatch(name(%#),[extract(%qA,2,1,~)]),strmatch(name(%#),[extract(%qA,4,1,~)]))]={@pemit %#=[ansi(hr,ERR:)]%bID %0 is not yours.};@pemit %#=[header(Details of ID %0)]%R[u(DISP`BOON.BANK.DETAILS,%qA)]%R[footer()]

&DISP`BOON.BANK.DETAILS #2885=[u(DISP`BBDETAILS.HEADER)]%R[iter([extract(%qA,3,1,~)],[ljust([before(%i0,/)],65)][ljust(after(%i0,/),13)],|,%R)]

&DISP`BBDETAILS.HEADER #2885=[ljust(Reason,65)][ljust(Boons,13)]%R%b[repeat(-,76)]

&CMD`BOON.ADD #2885=$+boons/add */*=*:@assert [gte(div(strlen(%1),2),1)]={@pemit %#=[ansi(hr,ERR:)]%bAmounts must be specified as #M#m#t in any order.};@assert [pmatch(%0)]={@pemit %#=[ansi(hr,ERR:)]%b%0 does not match a player.};[attrib_set(%va/UNAPP_BOONS,[setunion(xget(%va,UNAPP_BOONS),[name(%#)]~%0~%1~%2,|)])];@pemit %#=[ansi(hw,BOONS:)]%bInforming the harpies of your boon. This boon will not appear until the harpies have approved it.;@fo me=+bbpost 30/[name(%#)] to %0=%R[name(%#)] has given %0 %1 boons. The reason is below. Please use +boons/unapproved to see its ID and +boons/approve ID or +boons/deny ID\=Reason to approve or deny it.%R%RReason Given%R%2 

&CMD`BOON.ADD #2885=$+boons/claim */*=*:@assert [gte(div(strlen(%1),2),1)]={@pemit %#=[ansi(hr,ERR:)]%bAmounts must be specified as #M#m#t in any order.};@assert [pmatch(%0)]={@pemit %#=[ansi(hr,ERR:)]%b%0 does not match a player.};[attrib_set(%va/UNAPP_BOONS,[setunion(xget(%va,UNAPP_BOONS),%0~[name(%#)]~%1~%2,|)])];@pemit %#=[ansi(hw,BOONS:)]%bInforming the harpies of your claim. This boon will not appear until the harpies have approved it.;@fo me=+bbpost 30/[name(%#)] from %0=%R[name(%#)] claims %0 owes them %1 boons. The reason is below. Please use +boons/unapproved to see its ID and +boons/approve ID or +boons/deny ID\=Reason to approve or deny it.%R%RReason Given%R%2 

&CMD`BOON.CALLIN #2885=$+boons/callin */*=*:@assert [cand([t(setr(A,sql(u(SQL`GET_SPECIFIC_ID,%0),:,~)))],[strmatch(extract(%qA,4,1,~),[name(%#)])])]={@pemit %#=[ansi(hw,BOONS:)]%bEither %0 couldn't be found in the database or that's not a boon owed to you.};[setq(B,[inc(xget(#18,jobs_num))])];@trig #18/TRIG_CREATE=%#,#3632,2,[name(%#)] Calls in a Boon,[name(%#)] has called in boons from [extract(%qA,2,1,~)].%RAmount Requested: %1%RAmount Available: [extract(%qA,8,1,~)]M[extract(%qA,7,1,~)]m[extract(%qA,6,1,~)]t%R%RReason:%R%2,[setunion(,%# [pmatch(extract(%qA,4,1,~))] [xget(me,DATA`HARPIES)])];@wait 5=[setq(C,[u(#18/FN_FIND-JOB,%qB)])][attrib_set(%qC/BOON_ID,%0)][attrib_set(%qC/AMT,%1)][attrib_set(%qC/REASON,%2)];@pemit %#=[ansi(hw,BOONS:)]%bYou call in a boon from [extract(%qA,2,1,~)]. Job %qC has been opened for this.

&CMD`BOON.TRADE #2885=$+boons/trade */*=*/*:@assert [t(setr(a,sql(u(SQL`GET_SPECIFIC_ID,%1),:,~)))]={@pemit %#=[ansi(hr,ERR:)]%bCouldn't find that ID in the database. Is %1 right?};@assert [setq(b,[u(F`PARSE.BOON.AMT,%2)])][cand(if(t(extract(%qB,1,1)),gte(sub(extract(%qA,8,1,~),extract(%qB,1,1)),0),1),if(t(extract(%qB,2,1)),gte(sub(extract(%qA,7,1,~),extract(%qB,2,1)),0),1),if(t(extract(%qB,3,1)),gte(sub(extract(%qA,6,1,~),extract(%qB,3,1)),0),1))]={@pemit %#=[ansi(hr,ERR:)]%bYou don't have enough boons from that ID to trade that many.};@break [strmatch(extract(%qA,2,1,~),[name(%#)])]={@pemit %#=[ansi(hr,ERR:)]%bYou can't trade debt!};[u(F`TRADE.BOON,%1,%0,%2,%3,%qB,%qA)];@pemit %# [pmatch(%0)]=[ansi(hw,BOONS:)]%b[name(%#)] has traded some boons with [name(pmatch(%0))].

&F`TRADE.BOON #2885=[switch(t([setr(A,sql(u(SQL`CHECK_BOON_BALANCE_EXISTS,extract(%5,2,1,~),%1)))]),0,[sql(u(SQL`INSERT_BOON,extract(%q5,2,1,~),Trade from [name(%#)] for %3,%1,1,[if(t(extract(%4,3,1)),t,f)],[if(t(extract(%4,3,1)),t,f)],[if(t(extract(%4,3,1)),t,f)],TRADE - FROM [name(%#)] FOR %2 - [timefmt($a $m-$d-$Y)]))],1,[sql(u(SQL`UPDATE_EXISTING,setunion(extract(%qA,3,1,~),Trade from [name(%#)] for %3,|),add(extract(%qA,6,1,~),if(t(extract(%4,3,1)),extract(%4,3,1),0)),add(extract(%qA,7,1,~),if(t(extract(%4,2,1)),extract(%4,2,1),0)),add(extract(%qA,8,1,~),if(t(extract(%4,1,1)),extract(%4,1,1),0)),setunion(extract(%qA,9,1,~),TRADE - FROM [name(%#)] FOR %2 - [timefmt($a $m-$d-$Y)],|),extract(%qA,1,1,~)))])][sql(u(SQL`UPDATE_EXISTING,setunion(extract(%5,3,1,~),Trade to %1 for %3/%2,|),if(gte(sub(extract(%5,6,1,~),extract(%4,3,1)),0),sub(extract(%5,6,1,~),extract(%4,3,1)),0),if(gte(sub(extract(%5,7,1,~),extract(%4,2,1)),0),sub(extract(%5,7,1,~),extract(%4,2,1)),0),if(gte(sub(extract(%5,8,1,~),extract(%4,1,1)),0),sub(extract(%5,8,1,~),extract(%4,1,1)),0),setunion(extract(%5,9,1,~),TRADE - TO %1 %2 - [timefmt($a $m-$d-$Y)],|),%0))][u(F`CHECK.CAN.ARCHIVE,sub(extract(%5,6,1,~),extract(%4,3,1)),sub(extract(%5,7,1,~),extract(%4,2,1)),sub(extract(%5,8,1,~),extract(%4,1,1)),%0)]

&CMD`BOON.PRAXIS.OVERVIEW #2885=$+boons/praxis:@pemit %#=[header(Praxis Boon Overview)]%R[center(Trivial,26)][center(Minor,26)][center(Major,26)]%R%b[repeat(-,76)]%R[center(sql(SELECT SUM(trivial) FROM boons),26)][center(sql(SELECT SUM(minor) FROM boons),26)][center(sql(SELECT SUM(major) FROM boons),26)]%R%R[footer(Use +boons/search to find specific boons)]

&CMD`BOON.SEARCH #2885=$+boons/search *=*:@assert [cor(member(xget(me,DATA`HARPIES),%#),orflags(%#,Wr))];@switch [strmatch(%0,Credit)][strmatch(%0,Debt)]=10,{@pemit %#=[setq(B,[sql(u(SQL`GET_MY_BOONS_CREDIT,%1),:,~)])][header(Outstanding Boons Owed to %1)]%R[u(DISP`DETAILS.HEADER)]%R[switch([gt(words(%qB,:),10)],1,[iter(%qB,[if(lte(inum(0),10),[u(DISP`BOON.ROW.CREDIT,%i0)])],:,%R)][footer(Pg. 1/[u(F`CALC.PAGES,[words(%qB,:)])]. Use +boons/search Credit=%1/# for more)],0,[iter(%qB,[u(DISP`BOON.ROW.CREDIT,%i0)],:,%R)])]%R[footer()]},01,{@pemit %#=[setq(B,[sql(u(SQL`GET_MY_BOONS_DEBT,%1),:,~)])][header(Outstanding Boons %1 Owes)]%R[u(DISP`DETAILS.HEADER)]%R[switch([gt(words(%qB,:),10)],1,[iter(%qB,[if(lte(inum(0),10),[u(DISP`BOON.ROW.DEBT,%i0)])],:,%R)]%R[footer(Pg. 1/[u(F`CALC.PAGES,[words(%qB,:)])]. Use +boons/debt # for more)],0,[iter(%qA,[u(DISP`BOON.ROW.DEBT,%i0)],:,%R)]%R%R[footer()])]},{@pemit %#=[ansi(Hr,ERR:)]%bType must be one of Credit or Debt.}

&CMD`BOON.SEARCH.PAGE #2885=$+boons/search *=*/*:@assert [cor(member(xget(me,DATA`HARPIES),%#),orflags(%#,Wr))];@assert [cor(strmatch(%0,Credit),strmatch(%0,Debt))]={@pemit %#=[ansi(hr,ERR:)]%bType must be either Credit or Debt.};@assert [cand(isnum(%2),lte(%2,[u(F`CALC.PAGES,[words([setr(A,sql(u([switch([strmatch(%0,Credit)],1,SQL`GET_MY_BOONS_CREDIT,SQL`GET_MY_BOONS_DEBT)],%1),:,~))],:)])]))]={@pemit %#=[ansi(hr,ERR:)]%b%2 is either not a number or more than the pages available. There are [u(F`CALC.PAGES,[words(%qA,:)])] pages available.};@pemit [u(DISP`BOONS.PAGES,%qA,%#,[mul(10,%2)],[strmatch(%0,Credit)],%2,%1)]

&CMD`BOON.PRAXIS.UNAPPROVED #2885=$+boons/unapproved:@pemit %#=[header(Unapproved Boons)]%R[ljust(ID,5)][ljust(From,15)][ljust(To,15)][ljust(Reason,35)][ljust(Amount,8)]%R[iter(xget(%va,UNAPP_BOONS),[u(DISP`UNAPP.ROW,%i0,inum(0))],|,%R)]%R[footer()]

&DISP`UNAPP.ROW #2885=[ljust(%1,5)][ljust(extract(%0,1,1,~),15)][ljust(extract(%0,2,1,~),15)][ljust(mid(extract(%0,4,1,~),0,34),35)][ljust(extract(%0,3,1,~),8)]

&CMD`BOON.PRAXIS.APPROVE #2885=$+boons/approve *:@assert [cor(orflags(%#,Wr),member(xget(me,DATA`HARPIES),%#))];@assert [cand(isnum(%0),lte(%0,words(xget(%va,UNAPP_BOONS),|)))]={@pemit %#=[ansi(hr,ERR:)]%bEither %0 is not a number or the ID is invalid.};@switch [setq(A,[extract(xget(%va,UNAPP_BOONS),%0,1,|)])][setq(C,[u(F`PARSE.BOON.AMT,[extract(%qA,3,1,~)])])][t(setr(b,sql(u(SQL`CHECK_BOON_BALANCE_EXISTS,[extract(%qA,1,1,~)],[extract(%qA,2,1,~)]),:,~)))]=0,[sql(u(SQL`INSERT_BOON,[extract(%qa,1,1,~)],[extract(%qa,4,1,~)]/[extract(%qa,3,1,~)],[extract(%qa,2,1,~)],1,[extract(%qC,3,1)],[extract(%qC,2,1)],[extract(%qC,1,1)],CREATE - [extract(%qa,3,1,~)] - [timefmt($a $m-$d-$Y)]))][pemit(%#,[ansi(hw,BOONS:)]%bYou have approved [extract(%qa,1,1,~)]'s boon to [extract(%qa,2,1,~)] for [extract(%qa,4,1,~)])],1,[sql(u(SQL`UPDATE_EXISTING,[setunion([extract(%qB,3,1,~)],[extract(%qA,4,1,~)]/[extract(%qA,3,1,~)],|)],[add([extract(%qC,3,1)],[extract(%qB,6,1,~)])],[add(extract(%qC,2,1),extract(%qB,7,1,~))],[add(extract(%qC,1,1),extract(%qB,8,1,~))],[setunion(extract(%qB,9,1,~),ADD - [extract(%qa,3,1,~)] - [timefmt($a $m-$d-$Y)],|)]))][pemit(%#,[ansi(hw,BOONS:)]%bYou have approved [extract(%qa,1,1,~)]'s boon to [extract(%qa,2,1,~)] for [extract(%qa,4,1,~)])]

&CMD`BOON.PRAXIS.DENY #2885=$+boons/deny *=*:@assert [cor(orflags(%#,Wr),member(xget(me,DATA`HARPIES),%#))];@assert [cand(isnum(%0),lte(%0,words(xget(%va,UNAPP_BOONS),|)))]={@pemit %#=[ansi(hr,ERR:)]%bEither %0 is not a number or the ID is invalid.};[setq(A,[extract(xget(%va,UNAPP_BOONS),%0,1,|)])][attrib_set(%va/UNAPP_BOONS,[ldelete(xget(%va,UNAPP_BOONS),%0,|)])];@pemit %#=[ansi(BOONS:)]%bYou have denied [extract(%qA,1,1,~)]'s boon to [extract(%qa,2,1,~)] for [extract(%qa,4,1,~)];@trig #18/TRIG_CREATE=#2885,#3632,2,Denied Boon,%RDate: [timefmt($a $m-$d-$Y)]%RDenier: [name(%#)]%RAmount: [extract(%qA,3,1,~)]%RFrom Person: [extract(%qA,1,1,~)]%RTo Person: [extract(%qA,2,1,~)]%RReason for Boon: [extract(%qA,4,1,~)]%R%RThis boon was denied by [name(%#)] for the reason given:%R%0

&CMD`BOON.CLOSEJOB #2885=$+boons/closejob *=*:@assert [cor(member(xget(me,DATA`HARPIES),%#),orflags(%#,Wr))];@assert [t(setr(u(#18/FN_FIND-JOB,%0)))]={@pemit %#=[ansi(hr,ERR:)]%b%0 is not a valid job number.};@assert [strmatch(BOONS,[name(parent(%qU))])]={@pemit %#=[ansi(hr,ERR:)]%bCommand only works on the BOONS bucket.};@switch [before(%1,/)]=1,{@pemit %#=[u(F`CLOSE.BOON.JOB,[xget(%qT,BOON_ID)],[xget(%qT,AMT)],[xget(%qT,REASON)])][u(#18/FN_ADDSTAT_ART,parent(%qT),%qT)][u(#18/FN_ADDSTAT_ART,#20,%qT)]You have approved [name(%qT)], adding the comments: [trim(Boon transaction approved by Harpies)];@trigger #18/TRIG_BROADCAST=%qT,%#,APR;@trigger #18/TRIG_ADD=%qT,[u(#18/FN_STRTRUNC,trim(Boon transaction approved by Harpies),get(#18/BUFFER))],%#,APR;@trigger #18/TRIG_DESTROY=%qT},0,{@pemit %#=[u(#18/FN_ADDSTAT_ART,parent(%qT),%qT)][u(#18/FN_ADDSTAT_ART,#20,%qT)]You have denied [name(%qT)].;@trigger #18/TRIG_BROADCAST=%qT,%#,DNY;@trigger #18/TRIG_ADD=%qT,[u(#18/FN_STRTRUNC,trim(after(%1,/)),get(#18/BUFFER))],%#,DNY;@trigger #18/TRIG_DESTROY=%qT},{@pemit %#=[ansi(hr,ERR:)]%bYou didn't specify whether or not to approve or deny the job. Remember that 0 is deny and 1 is approve.}

&F`CLOSE.BOON.JOB #2885=[setq(Z,[sql(u(SQL`GET_SPECIFIC_ID,%0,:,~))])][setq(X,[u(F`PARSE.BOON.AMT,%1)])][setq(R,[sub(extract(%qZ,8,1,~),extract(%qX,1,1))])][setq(S,[sub(extract(%qZ,7,1,~),extract(%qX,2,1))])][setq(T,[sub(extract(%qZ,6,1,~),extract(%qX,3,1))])][sql(u(SQL`UPDATE_EXISTING,[setunion(extract(%qZ,3,1,~),%2/%1,|)],[if(gte(%qT,0),%qT,0)],[if(gte(%qS,0),%qS,0)],[if(gte(%qR,0),%qR,0)],[setunion(extract(%qZ,9,1,~),CALLED IN - %1 - [timefmt($a $m-$d-$Y)],|)],%0))][u(F`CHECK.CAN.ARCHIVE,%0,%qR,%qS,%qT)]

&F`CHECK.CAN.ARCHIVE #2885=[if(not(cor(gt(%1,0),gt(%2,0),gt(%3,0))),[sql(u(SQL`ARCHIVE_BOON_BANK,%0))])]

&F`PARSE.BOON.AMT #2885=[setq(A,[div(strlen(%0),2)])][setq(B,1)][iter(lnum(1,%qA),[switch([mid(%0,%qB,1)],M,[if(comp(M,[mid(%0,%qB,1)]),[setq(minor,[mid(%0,[sub(%qB,1)],1)])],[setq(major,[mid(%0,[sub(%qB,1)],1)])])],t,[setq(trivial,[mid(%0,[sub(%qB,1)],1)])])][setq(B,[add(%qB,2)])], ,[space(0)])]%q<major> %q<minor> %q<trivial>